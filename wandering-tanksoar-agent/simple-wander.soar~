## Move Operator Proposal
# If the task is tanksoar and the tank is not blocked in the forward
# direction, propose the move operator.
sp {propose*move*tanksoar
   (state <s> ^name tanksoar
              ^io.input-link.blocked.forward no)
-->
   (write |propose move forward|)
   (<s> ^operator <o> +)
   (<o> ^name move
        ^actions.move.direction forward)
}

## Turn Operator Proposal
# If the tank is blocked in the forward direction, and not blocked in the
# right or left directions, then propose the turn operator for the unblocked
# direction. Also create an indifferent preference for the operator.
# This operator will also turn on the radar and set the radar-power to 13.
sp {propose*turn*tanksoar
   (state <s> ^name tanksaor
              ^io.input-link.blocked.forward yes
              ^io.input-link.blocked.{ <direction> << right left >> } no)
-->
   (write (crlf) |propose move | <direction>)
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>
        ^actions.radar.switch on
        ^actions.radar-power.setting 13)
}
# If the tank is blocked in the forward direction, and in both the right or
# left directions, then propose the turn operator left.
sp {propose*turn-backward*tanksoar
   (state <s> ^name tanksoar
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^right yes ^left yes)
-->
   (write |propose move backward|)
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)
}

## Radar-off Operator Proposal
# If the radar is on but no energy, health, missiles and tanks visible,
# then propose the radar-off operator
sp {propose*radar-off*tanksoar
   (state <s> ^name tanksoar
              ^io.input-link.radar-status on
              ^io.input-link.radar <r>)
   (<r> -^<< energy health missile tank >>)
-->
   (write |propose turn off radar|)
   (<s> ^operator <o> +)
   (<o> ^name radar-off
        ^actions.radar.switch off)
}

## Radar-off Search Control
# If radar-off is proposed, then prefer it to move and turn.
sp {select*prefer-move*radar-off*tanksoar
   (state <s> ^name tanksoar
              ^operator.name radar-off <o1> +
              ^operator.name << move turn >> <o2> +)
-->
   (write |prefer turning radar off before moving or turning|)
   (<s> ^operator <o1> > <o2>)
}
              