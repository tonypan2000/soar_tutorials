##Wander Operator Proposal
# If there is no tank detected on radar, and the sound is silent, and there
# is no incoming, then propose the wander operator.
sp {propose*wander*tanksoar
   (state <s> ^name tanksoar
              ^io.input-link <in>)
   (<in> -^radar.tank
          ^sound silent
         -^incoming.<dir> yes)
-->
   (write |debug: tanksoar proposing wander|)
   (<s> ^operator <o> +)
   (<o> ^name wander)
}

## Wander: Move Operator Proposal
# If the wander operator is selected as the superoperator, and tank is not
# blocked in the forward direction, then propose the move operator.
sp {wander*propose*move
   (state <s> ^name wander
              ^io.input-link.blocked.forward no)
-->
   (write |debug: wander proposing move|)
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)
}

sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^ { << left right >> <direction> } no)
-->
   (write (crlf) |debug: wander proposing turning | <direction>)
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>
        ^actions.radar.switch on
        ^actions.radar-power.setting 13)
}

sp {wander*elaborate*move*radar-off
   :i-support
   (state <s> ^name wander
              ^operator <o>
              ^io.input-link <il>)
   (<il> ^radar-status on
         ^radar <r>)
   (<o> ^name move
        ^actions <a>)
  -(<r> ^ << energy health missiles tank >> <x>)
-->
   (write |debug: turn off radar because wander moving and there is nothing on radar|)
   (<a> ^radar.switch off)
}

sp {wander*propose*turn-backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (write |debug: wander proposing turn backward|)
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)
}
