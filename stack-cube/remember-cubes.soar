# Remember info about a cube if it has not been seen before
sp {propose-remember-cube
    (state <s> ^name stack-cube
               ^io.input-link.object <obj>)
    (<obj> ^cube-id <cid>
           ^object-id <oid>
           ^type led-cube
           ^pose <p>)
    (<p> ^x <px>
         ^y <py>)
    -(<s> ^cube.cube-id <cid>)
-->
    (<s> ^operator <o> + =)
    (<o> ^name remember-cube
         ^cube-id <cid>
         ^object-id <oid>
         ^pose <pose>)
    (<pose> ^x <px>
            ^y <py>)
    (write |remembered cube | <cid>)
}

sp {apply-remember-cube
    (state <s> ^operator <o>
               ^cube-count <cc>)
    (<o> ^name remember-cube
         ^cube-id <cid>
         ^object-id <oid>
         ^pose <p>)
    (<p> ^x <px>
         ^y <py>)
-->
    (<s> ^cube <c>
         ^cube-count <cc> -
                     (+ <cc> 1))
    (<c> ^cube-id <cid>
         ^object-id <oid>
         ^pose <pose>)
    (<pose> ^x <px>
            ^y <py>)
    (write |added new cube to memory|)
}

# break operator ties so remember cube has the highest priority
sp {cube-stack*compare*remember-cube
   (state <s> ^name stack-cube
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name remember-cube)
   (<op2> ^name <> remember-cube)
-->
   (<s> ^operator <op1> > <op2>)
}

# TODO: Update cube location if cube moved
