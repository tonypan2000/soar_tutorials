##Wander Operator Proposal
# If there is no tank detected on radar, and the sound is silent, and there
# is no incoming, then propose the wander operator.
sp {propose*wander*tanksoar
   (state <s> ^name tanksoar
             -^sound
              ^io.input-link <in>)
   (<in> -^radar.tank
         #  ^sound silent
         -^incoming.<dir> yes)
-->
   (write |debug: tanksoar proposing wander|)
   (<s> ^operator <o> +)
   (<o> ^name wander)
}

## Wander: Move Operator Proposal
# If the wander operator is selected as the superoperator, and tank is not
# blocked in the forward direction, then propose the move operator.
sp {wander*propose*move
   (state <s> ^name wander
              ^io.input-link.blocked.forward no)
-->
   (write |debug: wander proposing move|)
   (<s> ^operator <o> + =)
   (<o> ^name move
        ^actions.move.direction forward)
}

sp {wander*propose*turn
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes
        ^{ << left right >> <direction> } no)
-->
   (write (crlf) |debug: wander proposing turning | <direction>)
   (<s> ^operator <o> + =)
   (<o> ^name turn
        ^actions.rotate.direction <direction>
        ^actions.radar.switch on
        ^actions.radar-power.setting 13)
}

sp {wander*propose*turn-backward
   (state <s> ^name wander
              ^io.input-link.blocked <b>)
   (<b> ^forward yes ^left yes ^right yes)
-->
   (write |debug: wander proposing turn backward|)
   (<s> ^operator <o> +)
   (<o> ^name turn
        ^actions.rotate.direction left)
}

sp {wander*propose*record-sound
   (state <s> ^name wander
              ^io.input-link.sound <> silent)
-->
   (write |debug: propose record sound when wandering|)
   (<s> ^operator <o> + >, =) 
   (<o> ^name record-sound)
}

# sp {wander*apply*record-sound
#    (state <s> ^operator.name record-sound
#               ^io.input-link <il>)
#    (<il> ^sound <direction>
#          ^clock <clock>)
# -->
#    (write (crlf) |debug: wander apply record sound in direction | <direction>)
#    (<s> ^superstate.sound <sound>)
#    (<sound> ^direction <direction>
#             ^expire-time <+ <clock> 5>)
# }

sp {wander*apply*record-sound
   (state <s> ^operator.name record-sound
              ^io.input-link <il>
              ^superstate <ss>)
   (<il> ^sound <relative-direction>
         ^direction <absolute-direction>
         ^clock <clock>)
   (<ss> ^direction-map.<absolute-direction>.<relative-direction> <absolute-sound>)
-->
   (write (crlf) |debug: wander apply record sound in direction | <absolute-sound>)
   (<ss> ^sound <sound>)
   (<sound> ^absolute-direction <absolute-sound>
            ^expire-time (+ <clock> 5))
}
