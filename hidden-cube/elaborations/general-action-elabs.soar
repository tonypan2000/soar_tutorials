sp {apply*operator*create-action-command1
   (state <s> ^operator <o>
              ^io.output-link <out>
             -^io.output-link.<old-act>.status running)
   (<o> ^actions <act>)
   (<act> ^{ <att> << turn-in-place drive-forward >> } <value>)
-->
   # actions with multiple attributes (use copy of pointer to structure)
   (<out> ^<att> <value>)
}

sp {apply*operator*create-action-command2
   (state <s> ^operator <o>
              ^io.output-link <out>
             -^io.output-link.<old-act>.status running)
   (<o> ^actions <act>)
   (<act> ^{ <att> << move-lift move-head pick-up-object >> } <value>)
   (<value> ^<att2> <value2>)
-->
   # actions with 1 attribute (copy value directly)
   (<out> ^<att> <value3>)
   (<value3> ^<att2> <value2>)
}

sp {apply*operator*remove-command
   (state <s> ^operator.actions
              ^io.output-link <out>)
   (<out> ^<att> <value>)
   (<value> ^status complete)
-->
   (write (crlf) | REMOVE OPERATOR: | <att>)
   (<out> ^<att> <value> -)
}

sp {elaborate*state*io
   (state <s> ^superstate.io <io>)
-->
   (<s> ^io <io>)}

sp {elaborate*state*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)}
   